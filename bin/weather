#!/usr/bin/env perl

use strict;
use warnings;

use Furl;
use Getopt::Long;
use JSON;
use Pod::Usage;
use utf8;

use constant API_URL => 'https://api.wunderground.com/api/%s/%s/q/%s.json';
use constant RC_FILE => $ENV{'HOME'} . '/.weatherrc';

sub print_error {
    my $msg = shift;

    print {*STDERR} $msg;

    exit 1;
}

sub get_weather_data {
    my ( $opts, $key ) = @_;

    my $feature = $opts->{'forecast'} ? 'forecast10day' : 'conditions';
    my $url     = sprintf API_URL, $key, $feature, $opts->{'location'};

    my $furl = Furl->new();
    my $res  = $furl->get($url);

    print_error( $res->status_line . "\n" ) if ( !$res->is_success );

    my $json = $res->decoded_content;
    my $data = decode_json($json);

    return $data;
}

sub get_api_key {
    my $key = 0;

    open my $fh, "<", RC_FILE or print_error( "$!: " . RC_FILE . "\n" );
    chomp( $key = <$fh> );
    close $fh or print_error( "$!: " . RC_FILE . "\n" );

    return $key;
}

sub print_conditions {
    my $data = shift;

    my $info = $data->{'current_observation'};

    printf "%-12s %s\n", 'Location:', $info->{'display_location'}->{'full'};

    printf "%-12s %s\n", 'Latitude:', $info->{'display_location'}->{'latitude'};

    printf "%-12s %s\n",
      'Longitude', $info->{'display_location'}->{'longitude'};

    printf "%-12s %s m\n",
      'Elevation:', $info->{'display_location'}->{'elevation'};

    printf "%-12s %s\n", 'Condition:', $info->{'weather'};

    my $string = sprintf "%-12s %s °C\n", 'Temperature:', $info->{'temp_c'};
    my $bytestring = Encode::encode( "utf-8", $string );
    print $bytestring;

    printf "%-12s %s\n", 'Humidity:', $info->{'relative_humidity'};

    printf "%-12s %s km/h\n", 'Wind:', $info->{'wind_kph'};

    $string = sprintf "%-12s %s °C\n", 'Windchill:', $info->{'windchill_c'};
    $bytestring = Encode::encode( "utf-8", $string );
    print $bytestring;

    $string = sprintf "%-12s %s °C\n", 'Feels like:', $info->{'feelslike_c'};
    $bytestring = Encode::encode( "utf-8", $string );
    print $bytestring;

    printf "%-12s %s km\n", 'Visibility:', $info->{'visibility_km'};

    printf "%-12s %s mb\n", 'Pressure:', $info->{'pressure_mb'};

    exit 0;
}

sub print_forecast {
    my ( $days, $data ) = @_;

    my $info = $data->{'forecast'}{'txt_forecast'}{'forecastday'};

    $days = ( $days * 2 ) - 1;
    foreach ( 0 .. $days ) {
        my $txt = $info->[$_];
        next if ( !$txt );
        printf "%s: %s\n", $txt->{'title'}, $txt->{'fcttext_metric'};
    }

    exit 0;
}

sub print_locations {
    my $data = shift;

    my $info = $data->{'response'}{'results'};

    foreach ( @{$info} ) {
        printf "%s, %s: ZMW:%s\n",
          $_->{'city'}, $_->{'country_name'}, $_->{'zmw'};
    }

    exit 0;
}

sub print_weather {
    my $opts = shift;

    my $key  = get_api_key();
    my $data = get_weather_data( $opts, $key );

    print_error( $data->{'response'}{'error'}{'description'} . "\n" )
      if ( $data->{'response'}{'error'} );

    print_locations($data)
      if ( $data->{'response'}{'results'} );

    print_conditions($data)
      if ( $data->{'response'}{'features'}{'conditions'} );

    print_forecast( $opts->{'forecast'}, $data )
      if ( $data->{'response'}{'features'}{'forecast10day'} );

    return;
}

sub print_usage {
    pod2usage( -exitval => 255, -verbose => 0 );
    return;
}

sub print_help {
    pod2usage(
        -exitval  => 0,
        -verbose  => 99,
        -sections => 'SYNOPSIS|OPTIONS|PARAMETERS',
    );
    return;
}

sub print_man {
    pod2usage( -exitval => 0, -verbose => 2 );
    return;
}

sub get_location {
    my $furl = Furl->new();
    my $url  = 'https://ipinfo.io/city';
    my $res  = $furl->get($url);

    print_error( $res->status_line . "\n" ) if ( !$res->is_success );

    my $location = $res->content;
    $location =~ s/^\s+|\s+$//g;

    return $location;
}

sub run {
    my $opts;
    GetOptions(
        "forecast|f=i" => \$opts->{'forecast'},
        "help|h"       => \$opts->{'help'},
        "man|m"        => \$opts->{'man'},
    ) or print_usage();

    my $count = 0;
    foreach ( values %{$opts} ) {
        $count += 1 if ($_);
    }

    print_usage() if ( ( $opts->{'help'} || $opts->{'man'} ) && $count > 1 );
    print_man()  if ( $opts->{'man'} );
    print_help() if ( $opts->{'help'} );

    $opts->{'location'} = $ARGV[0] || get_location();

    print_weather($opts);

    return 0;
}

exit run();

__END__

=encoding utf8

=head1 NAME

weather - Get your weather from the underground.

=head1 SYNOPSIS

weather [--forecast DAYS] [LOCATION]

weather --help | --man

=head1 OPTIONS

=over 8

=item B<--forecast> DAYS

Forecast for the next n days (including today), max 10 days.

=back

=head1 PARAMETERS

=over 8

=item B<LOCATION>

City name, Wunderground PWS or ZMW code (default current location).

=back

=head1 DESCRIPTION

weather is a tool to get current conditions or forecast for a given location.

An Wunderground API key L<http://api.wunderground.com/weather/api> is needed
and must be provided in the user home, ~/.weatherrc.

=cut
