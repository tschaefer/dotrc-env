#!/usr/bin/env perl

use strict;
use warnings;

use Modern::Perl '2020';

use Carp;
use Net::Proxmox::VE;
use Getopt::Long qw(:config require_order);
use Pod::Usage;

my %cfg;

sub print_usage {
    pod2usage( -exitval => 255, -verbose => 0 );
    return;
}

sub print_help {
    pod2usage(
        -exitval  => 0,
        -verbose  => 99,
        -sections => 'SYNOPSIS|OPTIONS|PARAMETERS',
    );
    return;
}

sub print_man {
    pod2usage( -exitval => 0, -verbose => 2 );
    return;
}

sub parse_config {
    my $file = shift;

    open my $fh, "<", $file or croak("$!: $file");
    chomp( my @raw = <$fh> );
    close $fh or croak("$!: $file");

    foreach (@raw) {
        $cfg{$1} = $2 if (/^([\w-]+)\s*=\s*([\.\w-]+)\s*#?/);
    }

    return;
}

sub _proxmox_connect {
    my $p = Net::Proxmox::VE->new(
        {
            host     => $cfg{'host'},
            username => $cfg{'username'},
            password => $cfg{'password'},
            port     => $cfg{'port'},
            realm    => $cfg{'realm'},
            timeout  => 10,
            ssl_opts => { SSL_verify_mode => 0, verify_hostname => 0 },
        }
    );

    $p->login() or croak( sprintf "Can not log into %s", $cfg{'host'} );

    return $p;
}

sub _proxmox_get_node {
    my $p    = _proxmox_connect();
    my $resp = $p->get('/cluster/resources?type=vm');

    my $vm = $cfg{'vm'};

    my $node;
    foreach ( @{$resp} ) {
        if ( $_->{'vmid'} =~ /$vm/ ) {
            $node = $_->{'node'};
            last;
        }
    }

    croak( sprintf "No such VM %s", $vm ) if ( !$node );

    return $node;
}

sub _proxmox_get_uri {
    my $path = shift;

    my $node = _proxmox_get_node();

    my $uri = sprintf "/nodes/%s/qemu/%s", $node, $cfg{'vm'};
    $uri = sprintf "%s/%s", $uri, $path if ($path);

    return $uri;
}

sub _proxmox_call {
    my ( $method, $path, $data ) = @_;

    my $p   = _proxmox_connect();
    my $uri = _proxmox_get_uri($path);

    my $resp = undef;
    if ( $method =~ /get|delete/ ) {
        $resp = $p->$method($uri);
    }
    elsif ( $method =~ /post|put/ ) {
        $data ||= '';
        $resp = $p->$method( $uri, $data );
    }
    croak('Proxmox call failed.') if ( !defined $resp );

    return $resp;
}

sub status {
    my $resp = _proxmox_call( 'get', 'status/current' );
    printf "%s\n", $resp->{'qmpstatus'};

    return 0;
}

sub start {
    _proxmox_call( 'post', 'status/start' );

    return 0;
}

sub stop {
    _proxmox_call( 'post', 'status/stop' );

    return 0;
}

sub shutdown {
    _proxmox_call( 'post', 'status/shutdown' );

    return 0;
}

sub suspend {
    _proxmox_call( 'post', 'status/suspend' );

    return 0;
}

sub resume {
    _proxmox_call( 'post', 'status/resume' );

    return 0;
}

sub reset {
    _proxmox_call( 'post', 'status/reset' );

    return 0;
}

sub node {
    my $node = _proxmox_get_node();

    printf "%s\n", $node;

    return 0;
}

sub vnc {
    my ( $ip, $port ) = ( '0.0.0.0', 5900 );
    GetOptions(
        "ip|i=s"   => \$ip,
        "port|p=i" => \$port,
    ) or return print_usage();
    my $action = $ARGV[0] || '';

    if ( $action eq 'status' ) {
        my $resp =
          _proxmox_call( 'post', '/monitor', { command => 'info vnc' } );

        if ( $resp =~ /Server: ((?:[0-9]{1,3}\.){3}[0-9]{1,3}:[0-9]{1,5}) / ) {
            printf "enabled: %s\n", $1;
            return 0;
        }

        printf "disabled\n";
        return 1;
    }

    if ( $action eq 'enable' ) {
        my $resp =
          _proxmox_call( 'post', '/monitor',
            { command => sprintf "change vnc %s:%s", $ip, ( $port - 5900 ) } );

        return $resp !~ /failed/i ? 0 : 1;
    }

    return print_usage();
}

sub config {
    my $action = shift @ARGV || return print_usage();

    if ( $action eq 'show' ) {
        my $resp = _proxmox_call( 'get', '/config' );

        foreach my $key ( sort keys %{$resp} ) {
            my $value = $resp->{$key};
            $value =~ s/\n/ \/ /;
            printf "%12s: %s\n", $key, $value;
        }

        return 0;
    }

    if ( $action eq 'set' ) {
        my $set = shift @ARGV || return print_usage();
        my ( $key, $value ) = split /=/, $set;

        my $resp = _proxmox_call( 'post', '/config', { $key => $value } );

        return $resp !~ /failed/i ? 0 : 1;
    }

    return print_usage();
}

sub run {
    my $vm   = undef;
    my $file = "$ENV{HOME}/.config/proxmoxctlrc";

    GetOptions(
        "config|c=s" => \$file,
        "vm|v=s"     => \$vm,
    ) or return print_usage();

    my %ACTIONS = (
        help     => \&print_help,
        man      => \&print_man,
        config   => \&config,
        reset    => \&reset,
        resume   => \&resume,
        shutdown => \&shutdown,
        start    => \&start,
        status   => \&status,
        stop     => \&stop,
        suspend  => \&suspend,
        vnc      => \&vnc,
        node     => \&node,
    );
    my $action = $ARGV[0] || '';
    shift @ARGV;

    $action =~ s/-/_/g;
    my $sub = $ACTIONS{$action};
    return print_usage() if ( !$sub );

    return print_usage() if ( !$vm && $action !~ /man|help/ );

    parse_config($file);

    if ( $action !~ /(help|man)/ ) {
        $cfg{'vm'} = $vm if ($vm);
        return print_usage if ( !$cfg{'vm'} );
    }

    return &$sub();
}

exit run();

__END__

=encoding utf8

=head1 NAME

proxmoxctl - Control Proxmox Qemu/KVM machines

=head1 SYNOPSIS

proxmoxctl [--config FILE] [--vm VMID] ACTION

proxmoxctl ... reset | resume | start | status | stop | suspend | shutdown

proxmoxtcl ... config show

proxmoxctl ... config set KEY=VALUE

proxmoxctl ... vnc status

proxmoxctl ... vnc [--ip ADDRESS] [--port NUMBER] enable

proxmoxctl help | man

=head1 OPTIONS

=head2 base

=over 8

=item B<--config|-c> FILE

Path to configuration file, see L<DESCRIPTION>.

=item B<--vm|-v> VMID

Proxmox virtual machine id.

=back

=head2 vnc

=over 8

=item B<--ip|-i> ADDRESS

Ipv6 or Ipv4 address.

=item B<--port|-p> NUMBER

TCP port number.

=back

=head1 PARAMETERS

=head2 base

=over 8

=item B<reset>

Reset VM.

=item B<resume>

Resume VM.

=item B<shutdown>

Shutdown (ACPI) VM.

=item B<start>

Start VM.

=item B<status>

Get VM status.

=item B<stop>

Stop VM.

=item B<suspend>

Suspend VM.

=item B<config>

Show VM configuration.

=item B<vnc>

VNC subcommands.

=back

=head2 config

=over 8

=item B<show>

Show VM configuration.

=item B<set> KEY=VALUE

Set VM configuration by key value pair.

=back

=head2 vnc

=over 8

=item B<status>

VNC status.

=item B<enable>

Enable VNC and bind to given ip address (default: 0.0.0.0) and port number
(default: 5900).

=back

=head1 DESCRIPTION

Simple control tool for Proxmox Qemu/KVM machines.

The default configuration file is placed in ~/.config/proxmoxctlrc.

=head2 Example

    # Proxmox host instance to interact with.
    host = proxmox.acme.org

    # TCP port number used to by the Proxmox host instance.
    port = 8006

    # Authentication realm to request against.
    realm = pve

    # User name used for authentication.
    username = bofh

    # Pass word user for authentication.
    password = qwe123

    # Virtual machine id to control.
    vm = 4711

=cut
