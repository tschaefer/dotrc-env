#!/usr/bin/env perl

use strict;
use warnings;

use File::Basename;
use IPC::Run;
use Pod::Usage;
use Time::HiRes qw( sleep );

sub is_running {
    my @cmd = qw(/usr/bin/pgrep tint2);
    my $rc = IPC::Run::run( \@cmd, '>/dev/null', '2>&1' );
    return $rc ? 1 : 0;
}

sub start {
    return 0 if is_running();

    if ( my $pid = fork ) {
        sleep 0.5;
        return is_running();
    }
    else {
        my @cmd =
          [ '/usr/bin/tint2', '-c', $ENV{HOME} . '/.config/tint2/tint2rc' ];
        IPC::Run::run( @cmd, '>/dev/null', '2>&1' );
    }

    return 0;
}

sub stop {
    return 0 if !is_running();

    my @cmd = qw(/usr/bin/pkill -15 tint2 );
    IPC::Run::run( \@cmd, '>/dev/null', '2>&1' );

    sleep 0.5;
    return !is_running();
}

sub reload {
    return 0 if !is_running();

    my @cmd = qw(/usr/bin/pkill -10 tint2 );
    IPC::Run::run( \@cmd, '>/dev/null', '2>&1' );

    sleep 0.5;
    return is_running();
}

sub restart {
    stop();
    return start();
}

sub usage {
    pod2usage( -exitval => 255, -verbose => 0 );
    return;
}

sub help {
    pod2usage( -exitval => 0, -verbose => 1 );
    return;
}

sub man {
    pod2usage( -exitval => 0, -verbose => 2 );
    return;
}

sub run {
    my %ACTIONS = (
        is_running => \&is_running,
        start      => \&start,
        stop       => \&stop,
        reload     => \&reload,
        restart    => \&restart,
        help       => \&help,
        man        => \&man,
    );
    my $action = $ARGV[0];

    return usage() if ( !$action );

    $action =~ tr/-/_/;
    return usage() if ( !grep { /^$action$/ } keys %ACTIONS );

    my $sub = $ACTIONS{$action};
    return &$sub();
}

exit !run();

__END__

=head1 NAME

tint2ctl - Control tint2 ligthweigt panel/taskbar

=head1 SYNOPSIS

tint2ctl start | stop | restart | is-running

tint2ctl help | man

=head1 OPTIONS

=over 8

=item B<start>

Start (activate) tint2.

=item B<stop>

Stop (deactivate) tint2.

=item B<restart>

Stop and start tint2.

=item B<is-running>

Checks wheter tint2 is running (active).

=back

=head1 DESCRIPTION

B<tint2ctl> may be used to introspect and control the state of C<tint2>
lightweigt panel/taskbar.

=cut
